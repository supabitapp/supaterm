const std = @import("std");
const build_zig_zon = @import("build.zig.zon");

const linux_targets: []const std.Target.Query = &.{
    .{ .cpu_arch = .x86_64, .os_tag = .linux, .abi = .musl },
    .{ .cpu_arch = .aarch64, .os_tag = .linux, .abi = .musl },
};

const macos_targets: []const std.Target.Query = &.{
    .{ .cpu_arch = .x86_64, .os_tag = .macos },
    .{ .cpu_arch = .aarch64, .os_tag = .macos },
};

pub fn build(b: *std.Build) void {
    const target = b.standardTargetOptions(.{});
    const optimize = b.standardOptimizeOption(.{});
    const version = b.option([]const u8, "version", "Version string for release") orelse
        @as([]const u8, build_zig_zon.version);

    const exe_mod = b.createModule(.{
        .root_source_file = b.path("src/main.zig"),
        .target = target,
        .optimize = optimize,
        .link_libc = true,
        .strip = true,
    });

    const dep = b.dependency("ghostty", .{
        .target = target,
        .optimize = optimize,
        .@"emit-lib-vt" = true,
        // Not redundant: in lib-vt mode emit-xcframework defaults to "xcodebuild
        // on PATH" (true even via the CLT stub), which pulls in the iOS SDK at
        // configure time and breaks builds without full Xcode.
        .@"emit-xcframework" = false,
        .@"emit-macos-app" = false,
        .simd = false,
    });
    exe_mod.addImport(
        "ghostty-vt",
        dep.module("ghostty-vt"),
    );

    // Run
    {
        const run_step = b.step("run", "Run the app");
        const exe = b.addExecutable(.{
            .name = "supaterm-host",
            .root_module = exe_mod,
        });

        b.installArtifact(exe);
        const run_cmd = b.addRunArtifact(exe);
        run_cmd.step.dependOn(b.getInstallStep());
        if (b.args) |args| run_cmd.addArgs(args);
        run_step.dependOn(&run_cmd.step);
    }

    // Test
    {
        const test_step = b.step("test", "Run unit tests");
        const test_module = b.addModule("test", .{
            .root_source_file = b.path("src/test.zig"),
            .target = target,
            .optimize = optimize,
            .link_libc = true,
        });
        const test_dep = b.dependency("ghostty", .{
            .target = target,
            .optimize = optimize,
            .@"emit-lib-vt" = true,
            .@"emit-xcframework" = false,
            .@"emit-macos-app" = false,
            .simd = false,
        });
        test_module.addImport(
            "ghostty-vt",
            test_dep.module("ghostty-vt"),
        );
        const exe_unit_tests = b.addTest(.{
            .root_module = test_module,
        });

        const run_exe_unit_tests = b.addRunArtifact(exe_unit_tests);
        test_step.dependOn(&run_exe_unit_tests.step);
    }

    // Check for LSP integration
    {
        const check = b.step("check", "Check if supaterm-host compiles");
        const exe_check = b.addExecutable(.{
            .name = "supaterm-host",
            .root_module = exe_mod,
        });

        check.dependOn(&exe_check.step);
    }

    // Release step - cross-compile to all targets from any host
    {
        const release_step = b.step(
            "release",
            "Build release binaries for all platforms",
        );
        const release_targets = linux_targets ++ macos_targets;
        for (release_targets) |release_target| {
            const resolved = b.resolveTargetQuery(release_target);
            const release_mod = b.createModule(.{
                .root_source_file = b.path("src/main.zig"),
                .target = resolved,
                .optimize = .ReleaseSmall,
                .link_libc = true,
                .strip = true,
            });

            if (b.lazyDependency("ghostty", .{
                .target = resolved,
                .optimize = .ReleaseSmall,
                .@"emit-lib-vt" = true,
                .@"emit-xcframework" = false,
                .@"emit-macos-app" = false,
                .simd = false,
            })) |release_dep| {
                release_mod.addImport("ghostty-vt", release_dep.module("ghostty-vt"));
            }

            const release_exe = b.addExecutable(.{
                .name = "supaterm-host",
                .root_module = release_mod,
            });

            const os_name = @tagName(release_target.os_tag orelse .linux);
            const arch_name = @tagName(release_target.cpu_arch orelse .x86_64);
            const tarball_name = b.fmt("supaterm-host-{s}-{s}-{s}.tar.gz", .{ version, os_name, arch_name });

            const tar = b.addSystemCommand(&.{ "tar", "-czf" });

            const tarball = tar.addOutputFileArg(tarball_name);
            tar.addArg("-C");
            tar.addDirectoryArg(release_exe.getEmittedBinDirectory());
            tar.addArg("supaterm-host");

            const shasum = b.addSystemCommand(&.{"sha256sum"});
            shasum.addFileArg(tarball);
            const shasum_output = shasum.captureStdOut(.{});

            const install_tar = b.addInstallFile(tarball, b.fmt("dist/{s}", .{tarball_name}));
            const install_sha = b.addInstallFile(
                shasum_output,
                b.fmt("dist/{s}.sha256", .{tarball_name}),
            );
            release_step.dependOn(&install_tar.step);
            release_step.dependOn(&install_sha.step);
        }
    }
}
